
{>
	(c) 2023 Marcin Åšlusarczyk (quakcin)
			github.com/qs-lang/qs21
			
			Conditionals
}


{def> if-op> op> name>
	{if: {{name}-0}: {op}: {{name}-1}> 
		{if: {{name}-len}> ~> 4> {{name}-2}> 1}
	> 
		{if: {{name}-len}> ~> 4> {{name}-3}> 0}
	}
}

{def> if-eq> a> b>
	{if-op> ~> if-eq}
}

{def> if-gt> a> b>
	{if-op> )> if-gt}
}

{def> if-ls> a> b>
	{if-op> (> if-ls}
}

{def> if-and> a> b>
	{if: {a}> ~> 1>
		{if: {b}> ~> 1>
			{if: {if-and-len}> => 4> {if-and-2}> 1}
		>
			{if: {if-and-len}> => 4> {if-and-3}> 0}
		}
	>
		{if: {if-and-len}> => 4> {if-and-3}> 0}
	}
}

{def> if-or> a> b>
	{if: {a}> ~> 1>
		{if: {if-or-len}> => 4> {if-or-2}> 1}
	>
		{if: {b}> ~> 1>
			{if: {if-or-len}> => 4> {if-or-2}> 1}
		>
			{if: {if-or-len}> => 4> {if-or-3}> 0}
		}
	}
}

{def> if-in-range> n> min> max>
	{if: {n}> ]: {min}>
		{if: {n}> (: {max}>
			{if: {if-in-range-len}> => 5> {if-in-range-3}> 1}
		>
			{if: {if-in-range-len}> => 5> {if-in-range-4}> 0}
		}
	>
		{if: {if-in-range-len}> => 5> {if-in-range-4}> 0}
	}
}

{def> if-else>
	{priv: brk> i> last>
		{def> brk> 1}
		{def> i> 0}
		{def> last: {clc: {if-else-len}> -> 1}}

		{loop> brk>
			{if: {if-else-{i}}> ~> 1>
				{def> brk> 0}
				{if-else-{clc: {i}> +> 1}}
			>}
			
			{def> i: {clc: {i}> +> 2}}
			{if: {i}> ]: {last}>
				{def> brk> 0}
				{if-else-{last}}
			>}
		}
	}
}

