
{>
	(c) 2023 Marcin Åšlusarczyk (quakcin)
			github.com/qs-lang/qs21

			String
}

{def> nl: {chr> 10}}

{def> str-ltrim> str>
	{priv> notBlank> 
		{def> notBlank> 0}
		{do> i> 0: {strlen: {str}}>
			{if: {notBlank}> => 0>
				{def> notBlank: {if: {cc: {str}: {i}}> ~: {chr: 32}> 0> 1}}
			>}
			{if: {notBlank}> => 1>
				{cc: {str}: {i}}
			>}
		}
	}
}

{def> str-reverse> str>
  {do> i> 0: {strlen: {str}}>
		{cc: {str}: {clc: {strlen: {str}}> -: {i}> -> 1}}
  }
}

{def: str-rtrim> str>
  {str-reverse: {str-ltrim: {str-reverse: {str}}}}
}

{def: str-trim> str>
  {str-ltrim: {str-rtrim: {str}}}
}

{def> cc> str> idx>
	{chr: {asc: {str}: {idx}}}
}

{def> str-split-asc> dest> str> code>
	{arr-rm: {dest}}
	{priv> off> c> adr>
		{def> off> 0}
		{def: {dest}-0>}
		{do> i> 0: {strlen: {str}}>
			{def> c: {asc: {str}: {i}}}
			{def> adr: {dest}-{off}}
			{if: {c}> =: {code}>
				{def> off: {clc: {off}> +> 1}}
				{def: {dest}-{off}>}
			>
				{def: {adr}: {{adr}}{chr: {c}}}
			}
		}
		{def: {dest}-len: {clc: {off}> +> 1}}
	}
}

{def> str-split-chr> dest> str> char>
	{str-split-asc: {dest}: {str}: {chr: {char}}}
}

{def> str-substr> str> off> len>
	{do> i> 0: {len}>
		{cc: {str}: {clc: {off}> +: {i}}; }
	}
}

{def> str-split> dest> str> delim>
	{arr-rm: {dest}}
	{priv> delimlen> i> off> sub> adr> idx>
		{def> delimlen: {strlen: {delim}}}
		{def> i: {strlen: {str}}}
		{def> off> 0}
		{def: {dest}-0>}

		{loop> i> 
			{def> idx: {clc: {strlen: {str}}> -: {i}}}
			{def> sub: {str-substr: {str}: {idx}: {delimlen}} }

			{if: {sub}> ~: {delim}>
				{def> off: {clc: {off}> +> 1}}
				{def> i: {clc: {i}> -: {delimlen}}}
				{def: {dest}-{off}>}
			>
				{def> adr: {dest}-{off}}
				{def: {adr}: {{adr}}{cc: {str}: {idx}}}
				{def> i: {clc: {i}> -> 1}}
			}
		}
		{def: {dest}-len: {clc: {off}> +> 1}}
	}
}

{def> str-join> arr> sub>
	{do> i> 0: {{arr}-len}>
		{{arr}-{i}}
		{if: {clc: {i}> +> 1}> (: {{arr}-len}> 
			{sub}
		>}
	}
}

{def> str-replace> str> a> b>
	{str-split> str-replace-buff: {str}: {a}}
	{str-join> str-replace-buff: {b}}
	{arr-rm> str-replace-buff}
}

{def> str-index-of> dest> str> sub>
	{arr-rm: {dest}}
	{priv> sublen>
		{def> sublen: {strlen: {sub}}}
		{do> i> 0: {clc: {strlen: {str}}> -: {sublen}}>
			{if: {str-substr: {str}: {i}: {sublen}}> ~: {sub}>
				{def: {dest}-{{dest}-len}: {i}}
				{def: {dest}-len: {clc: {{dest}-len}> +> 1}}
			>}
		}
	}
}



{>
	(c) 2023 github.com/bandurama
}

{def> str-lower> str>
	{do> i> 0: {strlen: {str}}>
		{chr-is-upper: {cc: {str}: {i}}>
			{chr: {clc: {asc: {str}: {i}}> +> 32}> 0}
		>
			{cc: {str}: {i}}
		}
	}
}


{def> str-upper> str>
	{do> i> 0: {strlen: {str}}>
		{chr-is-lower: {cc: {str}: {i}}>
			{chr: {clc: {asc: {str}: {i}}> -> 32}> 0}
		>
			{cc: {str}: {i}}
		}
	}
}
