
{>
	(c) 2023 marcin Å›lusarczyk (quakcin)
			github.com/qs-lang/qs21
			
			arrays
}

{def> arr-do> iter> name> expr>
	{do> i> 0: {{name}-len}>
		{priv: {iter}>
			{def: {iter}: {v: {name}-{i}}}
			{arr-do-2}
		}
	}
}

{def> arr-puts> name>
	{puts: --------- len({name}) = {{name}-len} {nl}}
	{do> i> 0: {{name}-len}>
		{puts: {name}[{i}] = "{v: {name}-{i}}" {nl}}
	}
}

{def> arr-push> name> item>
	{def: {name}-{{name}-len}: {item}}
	{def: {name}-len: {clc: {{name}-len}> +> 1}}
}

{def> arr-pop> name>
	{priv> idx>
		{def> idx: {clc: {{name}-len}> -> 1}}
		{{name}-{idx}}
		{def: {name}-{idx}>}
		{def: {name}-len: {idx}}
	}
}

{def> arr-find> dest> name> str>
	{arr-rm: {dest}}
	{do> i> 0: {{name}-len}>
		{if: {{name}-{i}}> ~: {str}>
			{arr-push: {dest}: {i}}
		>}
	}
}

{def> arr-rm> name>
	{do> i> 0: {{name}-len}>
		{def: {name}-{i}>}
	}
	{def: {name}-len> 0}
}

{def> arr-def> name>
	{arr-rm: {name}}
	{def: {name}-len: {clc: {arr-def-len}> -> 1}}
	{do> i> 0: {{name}-len}>
		{def: {name}-{i}: {v: arr-def-{clc: {i}> +> 1}}}
	}
}

{def> arr-map> name> defn>
	{do> i> 0: {{name}-len}>
		{def: {name}-{i}: {{defn}: {v: {name}-{i}}}}
	}
}

